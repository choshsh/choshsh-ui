plugins {
  id 'org.springframework.boot' version '2.7.2'
  id 'io.spring.dependency-management' version '1.0.12.RELEASE'
  id 'java'
  id 'com.google.cloud.tools.jib' version '3.1.4'
  id "org.sonarqube" version "3.3"
  id("org.springdoc.openapi-gradle-plugin") version "1.3.4"
  id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'com.choshsh'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

swaggerSources {
  choshsh {
    inputFile = file('build/openapi.json')
    code {
      outputDir = file('docs')
      language = 'html2'
    }
  }
}

// 이미지 태그 설정
def imageTags = []
if (project.hasProperty("tag")) imageTags.add(project.getProperty("tag"))

jib {
  from {
    image = "amazoncorretto:17"
  }
  to {
    image = "choshsh/choshsh-ui"
    tags = imageTags
  }
  container {
    creationTime = "USE_CURRENT_TIMESTAMP"
    jvmFlags = ['-Dspring.profiles.active=prod', '-XX:+UseContainerSupport', '-Dserver.port=8080', '-Dfile.encoding=UTF-8']
    ports = ['8080']
    labels = [maintainer: 'choshsh <cho911115@gmail.com>']
    user = "1000:1000"
  }
}


dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'io.micrometer:micrometer-registry-prometheus:1.9.0'
  implementation 'org.springdoc:springdoc-openapi-ui:1.6.9'
  compileOnly 'org.projectlombok:lombok'
  developmentOnly 'io.netty:netty-resolver-dns-native-macos:4.1.77.Final:osx-aarch_64'
  runtimeOnly 'mysql:mysql-connector-java'
  runtimeOnly 'com.h2database:h2'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor 'org.projectlombok:lombok'
  testAnnotationProcessor 'org.projectlombok:lombok'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  swaggerCodegen 'org.openapitools:openapi-generator-cli:6.0.0'
}

tasks.named('test') {
  useJUnitPlatform()
}

tasks.named('bootRun') {
  String activeProfile = System.properties['spring.profiles.active']
  systemProperty "spring.profiles.active", activeProfile
}
